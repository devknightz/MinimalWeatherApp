/*
 * Copyright 2017 vinayagasundar
 * Copyright 2017 randhirgupta
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.android.application'

def globalConf = rootProject.ext


/* Load the API keys from Properties file */
Properties properties = new Properties()
properties.load(project.rootProject.file('keys.properties').newDataInputStream())

def releaseWeatherAPI = properties['releaseWeatherAPI']
def debugWeatherAPI = properties['debugWeatherAPI']



android {
    compileSdkVersion globalConf.compileSdkVersion
    buildToolsVersion globalConf.buildToolsVersion

    defaultConfig {
        def versionMajor = 0
        def versionMinor = 1
        def versionPatch = 0

        def versionFullName = "${versionMajor}.${versionMinor}.${versionPatch}"
        def versionCodeFinal = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100

        applicationId "you.devknights.minimalweather"

        minSdkVersion globalConf.minSdkVersion
        targetSdkVersion globalConf.targetSdkVersion

        versionCode versionCodeFinal
        versionName "${versionFullName}"

        multiDexEnabled true

        /*Enabled Vector Support*/
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        jackOptions {
            enabled true
        }
    }

    signingConfigs {
        release {
            storeFile file("${properties['keystore']}")
            storePassword "${properties['keystore_password']}"
            keyAlias "${properties['key_alias']}"
            keyPassword "${properties['key_password']}"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField("String", "OPEN_WEATHER_API_KEY", "\"$releaseWeatherAPI\"")
        }

        debug {
            minifyEnabled false

            buildConfigField("String", "OPEN_WEATHER_API_KEY", "\"$debugWeatherAPI\"")
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    Map<String, String> deps = globalConf.dependenices

    compile deps.appCompatLib
    compile deps.gridLayoutLib
    compile deps.constraintLib
    compile deps.archExtension
    compile deps.roomRuntime

    compile deps.playLocation

    compile deps.retrofit2
    compile deps.retrofit2Logger
    compile deps.gsonConverter

    annotationProcessor deps.archCompiler
    annotationProcessor deps.roomCompiler

    testCompile deps.junitLibs
    testCompile deps.mockWebServer
    testCompile( deps.archCoreTesting, {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    testCompile deps.mockitoCore
    testCompile deps.hamcrest

    androidTestCompile(deps.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    debugCompile deps.debugDB


}
